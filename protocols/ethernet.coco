version 1.0
endian be

include "ipv4.coco"
include "ipv6.coco"
include "arp.coco"

// ============================================================================
// Ethernet (IEEE 802.3)
// ============================================================================

// EtherType values (IEEE 802.3)
enum EtherType : u16 {
  IPV4 = 0x0800,
  ARP = 0x0806,
  VLAN = 0x8100,
  IPV6 = 0x86DD
}

// Ethernet header (14 bytes)
message ethernet_header {
  bytes dst_mac[6] [display: mac]
  bytes src_mac[6] [display: mac]
  EtherType ethertype
}

// 802.1Q VLAN frame (tag + inner payload)
layer message vlan_frame {
  bits[16] tci {
    bits[12] vlan_id
    bit dei               // Drop Eligible Indicator
    bits[3] pcp           // Priority Code Point
  }
  EtherType inner_ethertype
  bytes inner[] match inner_ethertype {
    EtherType.IPV4 -> ipv4_packet ip
    EtherType.IPV6 -> ipv6_packet ip6
    EtherType.ARP -> arp_packet arp
    _ -> {}
  }
}

// Ethernet frame base - common structure for all Ethernet frames
message ethernet_frame_base {
  ethernet_header header
  bytes payload[] match header.ethertype {
    EtherType.IPV4 -> ipv4_packet ip
    EtherType.IPV6 -> ipv6_packet ip6
    EtherType.ARP -> arp_packet arp
    EtherType.VLAN -> vlan_frame vlan
    _ -> {}
  }
}

// Standard Ethernet frame (IEEE 802.3)
// 64-byte minimum on wire, includes 4-byte FCS
layer message ethernet_frame extends ethernet_frame_base {
  bytes fcs[4]      // Frame Check Sequence (CRC32)
  pad[fill_to: 64]  // IEEE 802.3 minimum frame size (64 bytes)
}

// Ethernet frame with FCS stripped (common in PCAPs)
// Most NICs validate and strip the FCS before passing frames to the OS
layer message ethernet_no_fcs_frame extends ethernet_frame_base {
  pad[fill_to: 60]  // 64-byte minimum minus 4-byte FCS
}
